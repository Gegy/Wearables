buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"

def mod_file = getModFile()
version = getVersion("VERSION", mod_file)
def llibrary_version = getVersion("LLIBRARY_VERSION", mod_file)
group = "net.gegy1000"
archivesBaseName = "wearables"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.11.2-13.20.0.2282"
    runDir = "minecraft"
    mappings = "snapshot_20170415"
}

repositories {
	mavenCentral()
	maven {
		name = "mcmoddev"
		url = "http://maven.mcmoddev.com"
	}
    maven {
        url = "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    compile "net.ilexiconn:llibrary:$llibrary_version-1.11.2:dev"
    deobfCompile "mezz.jei:jei_1.11.2:4.3.1.260:api"
    runtime "mezz.jei:jei_1.11.2:4.3.1.260"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "llibrary_version", llibrary_version

    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version, "llibrary_version": llibrary_version
    }
        
    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

String getModFile() {
    String path = ""
    FileTree tree = fileTree(dir: 'src/main/java')
    tree.include '**/*.java'
    tree.visit {element ->
        if (element.file.isFile()) {
            element.file.eachLine { String s ->
                s = s.trim()
                if (s.startsWith("@Mod")) {
                    path = "src/main/java/$element.relativePath"
                }
            }
        }
    }
    return path
}

String getVersion(String type, String mod_file) {
    String major = "0"
    String revision = "0"
    String patch = "0"
    String prefix = "public static final String $type = \""
    File file = file(mod_file)
    file.eachLine { String s ->
        s = s.trim()
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2)
            String[] pts = s.split("\\.")

            major = pts[0]
            revision = pts[1]
            patch = pts[2]
        }
    }
    return "$major.$revision.$patch"
}


jar {
    manifest {
        attributes "FMLCorePlugin": "net.gegy1000.wearables.server.core.WearablesPlugin"
        attributes "FMLCorePluginContainsFMLMod": "true"
    }
}